import java.time.Year

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'idea'
    id 'java-library'
    id 'com.github.hierynomus.license' version "0.16.1" apply false
    id 'biz.aQute.bnd.builder' version '7.1.0' apply false
}

defaultTasks 'clean', 'build'

def projGroup = 'org.tools4j'
def projVersion = file('version.txt').text.trim()

def agronaVersion = '2.0.1'
def slf4jVersion = '2.0.16'
def junitVersion = '5.11.4'
def mockitoVersion = '5.15.2'
def assertjVersion = '3.27.3'
def hdrHistogramVersion = '2.2.2'
def chronicleVersion = '5.21.98'//fastest now??
//def chronicleVersion = '5.23.37'

static def getBuildJavaVersion() {
    def buildJavaVersion = JavaVersion.current().getMajorVersion()
    if (buildJavaVersion.indexOf('.') > 0) {
        buildJavaVersion = buildJavaVersion.substring(0, buildJavaVersion.indexOf('.'))
    }
    if (buildJavaVersion.indexOf('-') > 0) {
        buildJavaVersion = buildJavaVersion.substring(0, buildJavaVersion.indexOf('-'))
    }
    Integer.parseInt(buildJavaVersion)
}
int buildJavaVersion = getBuildJavaVersion()

ext {
    //gradle clean build publish -PossrhUsername=mterzer -PossrhPassword=xxx

    isReleaseVersion = !projVersion.endsWith('-SNAPSHOT')
    releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

    println name + " version=" + projVersion + " release=" + isReleaseVersion

    if (!project.hasProperty('ossrhUsername')) {
        ossrhUsername = ''
    }

    if (!project.hasProperty('ossrhPassword')) {
        ossrhPassword = ''
    }
}

def projectPom = {
    name = 'mmap'
    packaging = 'pom'
    // optionally artifactId can be defined here
    description = 'Memory mapped files used to implement off-heap low latency utilities such as queues'
    url = 'https://github.com/tools4j/mmap'

    scm {
        connection = 'scm:git:https://github.com/tools4j/mmap.git'
        developerConnection = 'scm:git:https://github.com/tools4j/mmap.git'
        url = 'https://github.com/tools4j/mmap.git'
    }

    licenses {
        license {
            name = 'The MIT License (MIT)'
            url = 'https://opensource.org/licenses/MIT'
        }
    }

    developers {
        developer {
            id = 'anton-anufriev'
            name = 'Antun Anufriev'
            email = 'anufriev@gmail.com'
            url = 'https://github.com/anton-anufriev'
        }
        developer {
            id = 'terzerm'
            name = 'Marco Terzer'
            email = 'terzerm@gmail.com'
            url = 'https://github.com/terzerm'
        }
    }
}

jar.enabled = false

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'biz.aQute.bnd.builder'

    dependencies {
        testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    }

    group = projGroup
    version = projVersion

    tasks.withType(Sign) {
        onlyIf {
            isReleaseVersion && gradle.taskGraph.hasTask(tasks.publish)
        }
    }

    tasks.withType(Jar).configureEach {
        enabled = true
        includeEmptyDirs = false
    }

    tasks.withType(JavaCompile) {
        if (JavaVersion.current().isJava9Compatible()) {
            options.compilerArgs.addAll(['--add-exports', 'java.base/java.lang.reflect=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'java.base/java.net=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'java.base/jdk.internal.ref=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED'])
        }
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    tasks.withType(Test) {

        enableAssertions = true
        jvmArgs('-Ddisable.thread.safety=true')

        jvmArgs('--add-opens', 'java.base/jdk.internal.misc=ALL-UNNAMED')
        jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')

        useJUnitPlatform {
            excludeTags 'perf'
        }

        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }

        reports.html.required = false // Disable individual test reports
    }

    task perfTest(type: Test) {
        maxParallelForks = 1

        enableAssertions = false
        jvmArgs('-Djvm.resource.tracing=false')
        jvmArgs('-Ddisable.thread.safety=true')

        if (JavaVersion.current().isJava9Compatible()) {
            jvmArgs('--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/java.net=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/jdk.internal.ref=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/java.util.zip=ALL-UNNAMED')
            jvmArgs('--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED')
        }

        useJUnitPlatform {
            includeTags 'perf'
        }
    }

    task profilerTest(type: Test) {
        maxParallelForks = 1

        enableAssertions = false
        jvmArgs('-Djvm.resource.tracing=false')
        jvmArgs('-agentpath:/Applications/JProfiler.app/Contents/Resources/app/bin/macos/libjprofilerti.jnilib=port=8849')

        useJUnitPlatform {
            includeTags 'perf'
        }
    }

    license {
        header = file("${rootDir}/etc/LICENSE.template")
        strictCheck = true
        ext.year = Year.now().value
        include "**/*.java"
        mapping {
            java='SLASHSTAR_STYLE'
        }
    }

    compileJava.dependsOn licenseFormat

    javadoc {
        title = '<h1>mmap</h1>'
        options.bottom = "<i>Copyright &#169; 2020-" + Year.now().getValue() + " tools4j.org (Marco Terzer, Anton Anufriev). All Rights Reserved.</i>"
        options.encoding = 'UTF-8'
        options.docEncoding = 'UTF-8'
        options.charSet = 'UTF-8'
        if (JavaVersion.current().isJava10Compatible()) {
            options.addBooleanOption 'html5', true
            options.links("https://docs.oracle.com/en/java/javase/${buildJavaVersion}/docs/api/")        }
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        archiveClassifier.set "test-${base.archivesName}"
        from sourceSets.test.output
    }

    configurations {
        tests
    }

    artifacts {
        tests testJar
    }
}

project(':mmap-region') {
    dependencies {
        api "org.agrona:agrona:${agronaVersion}"
        api "org.slf4j:slf4j-api:${slf4jVersion}"
        testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
    }

    jar {
        bundle {
            // workaround for https://github.com/bndtools/bnd/issues/6346
            properties.put("project.group", provider({project.group}))
            bnd """
                Automatic-Module-Name:  org.tools4j.mmap-region
                Bundle-Name:            org.tools4j.mmap-region
                Bundle-SymbolicName:    org.tools4j.mmap-region
                Implementation-Title:   mmap
                Implementation-Vendor:  tools4j.org
                Implementation-Version: ${projVersion}
                -exportcontents: org.tools4j.mmap, org.tools4j.mmap.*
                # Suppress headers that reduce reproducibility.
                -reproducible: true
                -noextraheaders: true
            """
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            mmapRegion(MavenPublication) {
                from components.java
                pom(projectPom)
            }
        }

        repositories {
            maven {
                url = !isReleaseVersion ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mmapRegion
    }
}

project(':mmap-queue') {
    dependencies {
        api "org.agrona:agrona:${agronaVersion}"
        api "org.slf4j:slf4j-api:${slf4jVersion}"
        api project(':mmap-region')

        testImplementation "org.hdrhistogram:HdrHistogram:${hdrHistogramVersion}"
        testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
        testImplementation "net.openhft:chronicle-queue:${chronicleVersion}"
    }

    jar {
        bundle {
            // workaround for https://github.com/bndtools/bnd/issues/6346
            properties.put("project.group", provider({project.group}))
            bnd """
                Automatic-Module-Name:  org.tools4j.mmap-queue
                Bundle-Name:            org.tools4j.mmap-queue
                Bundle-SymbolicName:    org.tools4j.mmap-queue
                Implementation-Title:   mmap
                Implementation-Vendor:  tools4j.org
                Implementation-Version: ${projVersion}
                -exportcontents: org.tools4j.mmap, org.tools4j.mmap.*
                # Suppress headers that reduce reproducibility.
                -reproducible: true
                -noextraheaders: true
            """
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            mmapQueue(MavenPublication) {
                from components.java
                pom(projectPom)
            }
        }

        repositories {
            maven {
                url = !isReleaseVersion ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mmapQueue
    }
}

project(':mmap-dictionary') {
    dependencies {
        api "org.agrona:agrona:${agronaVersion}"
        api "org.slf4j:slf4j-api:${slf4jVersion}"
        api project(':mmap-region')

        testImplementation "org.hdrhistogram:HdrHistogram:${hdrHistogramVersion}"
        testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
    }

    jar {
        bundle {
            bnd """
                Automatic-Module-Name:  org.tools4j.mmap-dictionary
                Bundle-Name:            org.tools4j.mmap-dictionary
                Bundle-SymbolicName:    org.tools4j.mmap-dictionary
                Implementation-Title:   mmap
                Implementation-Vendor:  tools4j.org
                Implementation-Version: ${projVersion}
                -exportcontents: org.tools4j.mmap, org.tools4j.mmap.*
                # Suppress headers that reduce reproducibility.
                -reproducible: true
                -noextraheaders: true
            """
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            mmapQueue(MavenPublication) {
                from components.java
                pom(projectPom)
            }
        }

        repositories {
            maven {
                url = !isReleaseVersion ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mmapQueue
    }
}


tasks.register('testReport', TestReport) {
    destinationDirectory = file("${rootDir}/reports/allTests")
    // Include the results from the `test` task in all sub-projects
    testResults.setFrom(subprojects*.test)
}

task copyCrashLogs(type: Copy) {
    from '.'
    include '**/hs_err*.log'
    include 'LICENSE'
    into 'build/crash_logs'

    includeEmptyDirs = false
}

wrapper {
    gradleVersion = '8.12.1'
    distributionType = 'ALL'
}